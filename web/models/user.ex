defmodule PhoenixToggl.User do
  use PhoenixToggl.Web, :model

  alias PhoenixToggl.{Workspace, WorkspaceUser, TimeEntry}

  @derive {Poison.Encoder, only: [:id, :first_name, :last_name, :email]}

  schema "users" do
    field :first_name, :string
    field :last_name, :string
    field :email, :string
    field :encrypted_password, :string

    field :password, :string, virtual: true

    has_many :owned_workspaces, Workspace
    has_many :workspace_users, WorkspaceUser
    has_many :workspaces, through: [:workspace_users, :user]
    has_many :time_entries, TimeEntry

    timestamps
  end

  @required_fields ~w(first_name email password)
  @optional_fields ~w(encrypted_password last_name)

  @doc """
  Creates a changeset based on the `model` and `params`.

  If no params are provided, an invalid changeset is returned
  with no validation performed.
  """
  def changeset(model, params \\ %{}) do
    model
    |> cast(params, @required_fields, @optional_fields)
    |> validate_format(:email, ~r/@/)
    |> validate_length(:password, min: 5)
    |> unique_constraint(:email, message: "Email already taken")
    |> generate_encrypted_password
  end

  defp generate_encrypted_password(current_changeset) do
    case current_changeset do
      %Ecto.Changeset{valid?: true, changes: %{password: password}} ->
        put_change(current_changeset, :encrypted_password, Comeonin.Bcrypt.hashpwsalt(password))
      _ ->
        current_changeset
    end
  end
end
